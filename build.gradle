buildscript {
    Properties versionProps = new Properties()
    versionProps.load((InputStream) project.file("${projectDir}/versions.properties").newDataInputStream())
    ext { versions = versionProps }
    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "org.elasticsearch.gradle:build-tools:${versionProps.elasticsearch}"
        classpath "gradle.plugin.com.hierynomus.gradle.plugins:license-gradle-plugin:0.15.0"
    }
}

configure(allprojects) { p ->

    apply plugin: 'java'
    apply plugin: 'idea'

    group 'com.github.papahigh.phonetic'
    version '1.0-SNAPSHOT'

    def commonCompilerArgs =
            ["-Xlint:serial", "-Xlint:cast", "-Xlint:classfile", "-Xlint:dep-ann",
             "-Xlint:divzero", "-Xlint:empty", "-Xlint:finally", "-Xlint:overrides",
             "-Xlint:path", "-Xlint:static", "-Xlint:try", "-Xlint:-options"]

    compileJava.options*.compilerArgs = commonCompilerArgs +
            ["-Xlint:varargs", "-Xlint:fallthrough", "-Xlint:processing", "-Xlint:rawtypes",
             "-Xlint:deprecation", "-Xlint:unchecked", "-Werror"]

    compileTestJava.options*.compilerArgs = commonCompilerArgs +
            ["-Xlint:-varargs", "-Xlint:-fallthrough", "-Xlint:-rawtypes",
             "-Xlint:-deprecation", "-Xlint:-unchecked", "-Xlint:-processing"]

    compileJava {
        options.encoding = 'UTF-8'
    }

    compileTestJava {
        options.encoding = 'UTF-8'
    }

    java {
        disableAutoTargetJvm()
    }

    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
    }
}


configure(allprojects - project(':benchmark')) {

    apply plugin: "com.github.hierynomus.license"

    license {
        header = rootProject.file("HEADER.txt")
        ext.year = Calendar.getInstance().get(Calendar.YEAR)
        ext.developer = 'Nikolay Papakha'
        ignoreFailures false
        strictCheck true
        mapping {
            java = 'SLASHSTAR_STYLE'
        }
        excludes([
                "**/*.txt",
                "**/*.csv",
                "**/*.properties",
                "**/*.xml",
                "**/*.yml"
        ])
    }

}